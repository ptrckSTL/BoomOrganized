apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'app.cash.molecule'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.simplemobiletools.smsmessenger"
        minSdkVersion 29
        targetSdkVersion 33
        versionCode 78
        versionName "5.18.1"
        setProperty("archivesBaseName", "sms-messenger")
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        if (keystorePropertiesFile.exists()) {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    flavorDimensions "variants"
    productFlavors {
        core {}
        fdroid {}
        prepaid {}
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs = ["-Xcontext-receivers"]

    }

    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.3.2'
    }
    packagingOptions {
        resources {
            excludes += "META-INF/DEPENDENCIES"
        }
    }
    namespace 'com.simplemobiletools.smsmessenger'
    lint {
        abortOnError false
        checkReleaseBuilds false
    }
}

dependencies {
    implementation 'com.google.android.gms:play-services-auth:20.3.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.6.4'

    // https://developers.google.com/gsuite/guides/android
    // https://mvnrepository.com/artifact/com.google.api-client/google-api-client-android
    implementation('com.google.api-client:google-api-client-android:2.0.0') {
        exclude group: 'com.google.guava'
        exclude module: 'guava-jdk5'
    }

    implementation('com.google.apis:google-api-services-drive:v3-rev20220815-2.0.0') {
        exclude group: 'org.apache.httpcomponents'
    }

    // https://mvnrepository.com/artifact/com.google.apis/google-api-services-sheets
    implementation 'com.google.apis:google-api-services-sheets:v4-rev20220927-2.0.0'

    implementation 'org.greenrobot:eventbus:3.3.1'
    implementation 'com.github.tibbi:IndicatorFastScroll:4524cd0b61'
    implementation 'com.github.tibbi:android-smsmms:33fcaf94d9'
    implementation "me.leolin:ShortcutBadger:1.1.22"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.googlecode.ez-vcard:ez-vcard:0.11.3'
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1"
    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation "androidx.compose.ui:ui:$compose_ui_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    implementation 'androidx.compose.material:material:1.3.1'
    implementation 'androidx.lifecycle:lifecycle-process:2.5.1'
    implementation 'io.coil-kt:coil-compose:2.3.0'
    implementation "io.coil-kt:coil-gif:2.3.0"
    implementation 'com.github.doyaaaaaken:kotlin-csv-jvm:1.7.0'
    implementation 'androidx.work:work-runtime-ktx:2.7.1'
    implementation project(path: ':commons')


    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"

    kapt 'androidx.room:room-compiler:2.5.0'
    implementation 'androidx.room:room-ktx:2.5.0'
    implementation 'nl.dionsegijn:konfetti-compose:2.0.2'

    annotationProcessor "androidx.room:room-compiler:2.5.0"
}
